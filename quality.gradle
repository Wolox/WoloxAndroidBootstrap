apply plugin: 'checkstyle'
apply plugin: 'com.chaitanyapramod.findbugs-android'

configurations {
    ktlint
}

buildscript {
    ext.ktlint_version = '0.27.0'
}

dependencies {
    ktlint "com.github.shyiko:ktlint:$ktlint_version"
}

findbugs {
    toolVersion '3.0.1'
    ignoreFailures false
    effort 'max'
    reportLevel 'high'
    reportsDir = file("$project.buildDir/reports")
}

task checkstyle(type: Checkstyle) {
    description 'Run checkstyle'
    group 'verification'

    showViolations = true
    configFile file('quality/checkstyle/checkstyle.xml')
    // Expose ${configDir} in Checkstyle files
    configProperties = ['configDir': file('quality/checkstyle')]
    // empty classpath
    classpath = files()

    source 'src/main/java'
    include '**/*.java'
    exclude '**/gen/**'
    exclude '**/R.java'
    exclude '**/BuildConfig.java'

    ignoreFailures = false

    reports {
        xml.enabled = false
        html.enabled = true
        html.destination = file("$project.buildDir/reports/checkstyle.html")
    }
}

task ktlint(type: JavaExec) {
    group = 'verification'
    description = 'Check Kotlin code style'
    main = 'com.github.shyiko.ktlint.Main'
    classpath = configurations.ktlint
    args 'src/**/*.kt', "--reporter=checkstyle", "output=${project.buildDir}/reports/ktlint.xml"
}

task ktlintFormat(type: JavaExec) {
    group = 'formatting'
    description = 'Fix Kotlin code style deviations'
    main = 'com.github.shyiko.ktlint.Main'
    classpath = configurations.ktlint
    args '-F', 'src/**/*.kt'
}

preBuild.dependsOn 'checkstyle', 'ktlintFormat'

check.dependsOn 'checkstyle', 'findbugsProductionRelease', 'ktlintFormat', 'ktlint'