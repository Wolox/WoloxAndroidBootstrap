apply from: '../jacoco.gradle'
apply plugin: 'com.android.application'
apply from: 'keystore.gradle'
apply from: '../quality.gradle'
apply from: '../scripts/warp/warp_task.gradle'
apply plugin: 'idea'

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.3'

    defaultConfig {
        applicationId "ar.com.wolox.android.example"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "1.0.0"

        setProperty("archivesBaseName", "WoloxApp-$versionName") // Generated APK file name
    }

    signingConfigs {
        debug {
            storeFile file('keystore/debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
        release {
            storeFile release_keystore
            storePassword store_password
            keyAlias key_alias
            keyPassword key_password
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            signingConfig signingConfigs.debug
            testCoverageEnabled = true
        }
    }

    productFlavors {
        stage {
            applicationId defaultConfig.applicationId + ".stage"
            versionName defaultConfig.versionName + "-stage"
            resValue "string", "app_name", "[Stage] WoloxApp"
        }

        production {
            resValue "string", "app_name", "WoloxApp"
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    lintOptions {
        abortOnError false
    }
}

buildscript {
    ext.support_library_version = '25.3.1'
    ext.butterknife_version = '8.6.0'
    ext.chuck_version = '1.0.4'
    ext.leak_canary_version = '1.5.1'
    ext.wolmo_version = 'v1.1.0'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // Wolmo
    compile "com.github.Wolox:wolmo-core-android:$wolmo_version"
    compile "com.github.Wolox:wolmo-networking-android:$wolmo_version"

    // Android support libraries
    compile "com.android.support:support-compat:$support_library_version"
    compile "com.android.support:recyclerview-v7:$support_library_version"

    // Third-party
    compile "com.jakewharton:butterknife:$butterknife_version"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterknife_version"
    debugCompile "com.readystatesoftware.chuck:library:$chuck_version"
    releaseCompile "com.readystatesoftware.chuck:library-no-op:$chuck_version"
    debugCompile "com.squareup.leakcanary:leakcanary-android:$leak_canary_version"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leak_canary_version"

    // Testing dependencies
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leak_canary_version"
}
