apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'idea'
apply plugin: 'com.hiya.jacoco-android'
apply from: 'keystore.gradle'
apply from: '../quality.gradle'
apply from: '../scripts/warp/warp_task.gradle'
apply from: '../scripts/sanity/sanity_task.gradle'

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.2'

    defaultConfig {
        applicationId "ar.com.wolox.android.example"
        minSdkVersion 19
        targetSdkVersion 29
        multiDexEnabled true
        versionCode 1
        versionName "1.0.0"

        setProperty("archivesBaseName", "WoloxApp-$versionName") // Generated APK file name
    }

    signingConfigs {
        debug {
            storeFile file('keystore/debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
        release {
            storeFile release_keystore
            storePassword store_password
            keyAlias key_alias
            keyPassword key_password
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            signingConfig signingConfigs.debug
            testCoverageEnabled = true
        }
    }

    // Change the "default" to whatever fits. At least one 'dimension' is required by gradle.
    flavorDimensions "default"

    productFlavors {
        stage {
            applicationId defaultConfig.applicationId + ".stage"
            versionName defaultConfig.versionName + "-stage"
            resValue "string", "app_name", "[Stage] WoloxApp"
        }

        production {
            resValue "string", "app_name", "WoloxApp"
        }
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    buildFeatures {
        dataBinding true
    }
}

jacocoAndroidUnitTestReport {
    excludes = defaultExcludes + ['**/*_Factory.class']
}

buildscript {
    ext.androidx_core_version = '1.3.2'
    ext.androidx_recycler_version = '1.1.0'
    ext.androidx_material_version = '1.2.0-alpha03'
    ext.constraint_version = '2.0.0-beta4'
    ext.dagger_version = '2.28.1'
    ext.wolmo_core_version = '4.1.1'
    ext.wolmo_networking_version = '4.0.1'
    ext.leak_canary_version = '1.6.2'
    ext.chuck_version = '1.1.0'
    ext.powermock_version = '1.7.0'
    ext.okhhtp_login_interceptor_version = '4.7.2'
    ext.junit_version = '4.13'
    ext.mockito_inline_version = '2.13.0'
    ext.assertj_version = '3.9.0'
    ext.android_ktx_version = '1.1.0'
    ext.mockito_kotlin_version = "2.2.0"
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Wolmo
    implementation "com.github.Wolox:wolmo-core-android:4.1.1"
    implementation "com.github.Wolox:wolmo-networking-android:$wolmo_networking_version"

    // Android libraries
    implementation "androidx.core:core-ktx:$androidx_core_version"
    implementation "androidx.recyclerview:recyclerview:$androidx_recycler_version"
    implementation "androidx.constraintlayout:constraintlayout:$constraint_version"
    implementation "com.google.android.material:material:$androidx_material_version"

    // Dagger
    implementation "com.google.dagger:dagger:$dagger_version"
    implementation "com.google.dagger:dagger-android-support:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    kapt "com.google.dagger:dagger-android-processor:$dagger_version"

    // Third-party
    implementation "com.squareup.okhttp3:logging-interceptor:$okhhtp_login_interceptor_version"
    debugImplementation "com.readystatesoftware.chuck:library:$chuck_version"
    releaseImplementation "com.readystatesoftware.chuck:library-no-op:$chuck_version"
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leak_canary_version"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leak_canary_version"

    // Testing dependencies
    testImplementation "junit:junit:$junit_version"
    testImplementation "org.assertj:assertj-core:$assertj_version"
    testImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leak_canary_version"
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:$mockito_kotlin_version"

    // Android KTX
    implementation "androidx.core:core-ktx:$android_ktx_version"
    implementation "androidx.fragment:fragment-ktx:$android_ktx_version"
}
