apply plugin: 'com.android.application'
apply plugin: 'idea'
apply from: 'keystore.gradle'
apply from: '../jacoco.gradle'
apply from: '../quality.gradle'
apply from: '../scripts/warp/warp_task.gradle'

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'

    defaultConfig {
        applicationId "ar.com.wolox.android.example"
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 1
        versionName "1.0.0"

        setProperty("archivesBaseName", "WoloxApp-$versionName") // Generated APK file name
    }

    signingConfigs {
        debug {
            storeFile file('keystore/debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
        release {
            storeFile release_keystore
            storePassword store_password
            keyAlias key_alias
            keyPassword key_password
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            signingConfig signingConfigs.debug
            testCoverageEnabled = true
        }
    }

    productFlavors {
        stage {
            applicationId defaultConfig.applicationId + ".stage"
            versionName defaultConfig.versionName + "-stage"
            resValue "string", "app_name", "[Stage] WoloxApp"
        }

        production {
            resValue "string", "app_name", "WoloxApp"
        }
    }

    lintOptions {
        abortOnError false
    }
}

buildscript {
    ext.support_library_version = '26.1.0'
    ext.dagger_version = '2.11-rc2'
    ext.butterknife_version = '8.6.0'
    // TODO Update wolmo version when merging both wolmo-core and wolmo-networking
    ext.wolmo_version = 'dagger-android-SNAPSHOT'
    ext.leak_canary_version = '1.5.1'
    ext.chuck_version = '1.0.4'
    ext.powermock_version = '1.7.0'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // Wolmo
    compile "com.github.Wolox:wolmo-core-android:$wolmo_version"
    compile "com.github.Wolox:wolmo-networking-android:$wolmo_version"

    // Android support libraries
    compile "com.android.support:support-compat:$support_library_version"
    compile "com.android.support:recyclerview-v7:$support_library_version"
    compile "com.android.support:design:$support_library_version"

    // Dagger
    compile "com.google.dagger:dagger:$dagger_version"
    compile "com.google.dagger:dagger-android-support:$dagger_version"
    annotationProcessor "com.google.dagger:dagger-compiler:$dagger_version"
    annotationProcessor "com.google.dagger:dagger-android-processor:$dagger_version"

    // Third-party
    compile "com.jakewharton:butterknife:$butterknife_version"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterknife_version"
    compile "com.squareup.okhttp3:logging-interceptor:3.7.0"
    debugCompile "com.readystatesoftware.chuck:library:$chuck_version"
    releaseCompile "com.readystatesoftware.chuck:library-no-op:$chuck_version"
    debugCompile "com.squareup.leakcanary:leakcanary-android:$leak_canary_version"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leak_canary_version"

    // Testing dependencies
    testCompile "junit:junit:4.12"
    testCompile "org.mockito:mockito-inline:2.8.47"
    testCompile "org.assertj:assertj-core:3.8.0"
    testCompile "org.powermock:powermock-module-junit4:$powermock_version"
    testCompile "org.powermock:powermock-api-mockito2:$powermock_version"
    testCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leak_canary_version"
}
