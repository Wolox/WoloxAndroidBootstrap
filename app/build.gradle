apply from: '../jacoco.gradle'
apply plugin: 'com.android.application'
apply from: 'keystore.gradle'
apply from: '../quality.gradle'
apply from: '../scripts/warp/warp_task.gradle'
apply plugin: 'idea'

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'

    defaultConfig {
        applicationId "ar.com.wolox.android.example"
        minSdkVersion 19
        targetSdkVersion 26
        versionCode 1
        versionName "1.0.0"

        setProperty("archivesBaseName", "WoloxApp-$versionName") // Generated APK file name
    }

    signingConfigs {
        debug {
            storeFile file('keystore/debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
        release {
            storeFile release_keystore
            storePassword store_password
            keyAlias key_alias
            keyPassword key_password
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            signingConfig signingConfigs.debug
            testCoverageEnabled = true
        }
    }

    // Change the "default" to whatever fits. At least one 'dimension' is required by gradle.
    flavorDimensions "default"

    productFlavors {
        stage {
            applicationId defaultConfig.applicationId + ".stage"
            versionName defaultConfig.versionName + "-stage"
            resValue "string", "app_name", "[Stage] WoloxApp"
        }

        production {
            resValue "string", "app_name", "WoloxApp"
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

buildscript {
    ext.support_library_version = '26.1.0'
    ext.butterknife_version = '8.6.0'
    ext.chuck_version = '1.0.4'
    ext.leak_canary_version = '1.5.1'
    ext.wolmo_version = 'v1.3.1'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Wolmo
    implementation "com.github.Wolox:wolmo-core-android:$wolmo_version"
    implementation "com.github.Wolox:wolmo-networking-android:$wolmo_version"

    // Android support libraries
    implementation "com.android.support:support-compat:$support_library_version"
    implementation "com.android.support:recyclerview-v7:$support_library_version"

    // Third-party
    implementation "com.jakewharton:butterknife:$butterknife_version"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterknife_version"
    debugImplementation "com.readystatesoftware.chuck:library:$chuck_version"
    releaseImplementation "com.readystatesoftware.chuck:library-no-op:$chuck_version"
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leak_canary_version"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leak_canary_version"

    // Testing dependencies
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:1.10.19'
    testImplementation 'org.hamcrest:hamcrest-library:1.3'
    testImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leak_canary_version"
}
